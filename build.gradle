plugins {
    id 'java'
    id 'idea'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

bukkit {
    name = 'Stonks'
    version = '2.2-' + getGitHash()
    apiVersion = '1.14'
    main = 'dev.tycho.stonks.Stonks'
    authors = ['TychoVI', 'Tsarcasm', 'JRoy']
    softDepend = ['ChestShop', 'Essentials']
    depend = ['PluginLibrary', 'Vault']
    commands {
        company {
            description = 'Main Stonks Command'
            aliases = ['stonks', 'comp']
            usage = '/company'
        }
        cc {
            description = '/cc'
            usage = '/cc'
        }
        ccr {
            description = '/ccr'
            usage = '/ccr'
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://ci.ender.zone/plugin/repository/everything/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"
    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        exclude module: 'bukkit'
    }
    compileOnly files('libs/ChestShop.jar')
    compileOnly("net.ess3:EssentialsX:2.17.0") {
        exclude module: 'bukkit'
    }
    compileOnly "com.github.schlatt-co:PluginLibrary:87bf88cde4"
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

String getGitHash() {
    def output = new ByteArrayOutputStream()
    exec {
        standardOutput = output // Only the std, not errors as it's not to worry about as long as it's a repo
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
    }
    return output.toString().trim()
}